plugins {
    id 'java'
    id 'io.qameta.allure' version '2.8.1'
}

group 'com.epam'
version '1.0.1'

sourceCompatibility = 1.8

def cucumberVersion = '4.8.0'
def allureVersion = '2.13.3'
def selenideVersion = '5.11.1'
def restAssuredVersion = '4.3.0'
def gsonVersion = '2.8.2'
def lombokVersion = '1.18.12'
def assertjVersion = '3.10.0'
def slf4jVersion = '1.7.25'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "io.cucumber:cucumber-java8:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "io.qameta.allure:allure-cucumber4-jvm:${allureVersion}"

    testImplementation "com.codeborne:selenide:${selenideVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            systemProperty("allure.results.directory", "build/allure-results")

            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'io.qameta.allure.cucumber4jvm.AllureCucumber4Jvm',
                    '--glue', 'stepDefinitions', 'src/test/resources']
        }
    }
}

allure {
    version = allureVersion
    autoconfigure = true
    aspectjweaver = true
}

cucumber.finalizedBy 'allureReport'